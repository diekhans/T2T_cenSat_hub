import os
from os import path as osp
import glob
import shutil
import tempfile
from pycbio.sys import fileOps
import pipettor

asmName = "cenSat1"

# must create cenSat1.seqs with the list of contigs

resultsDir = "/nanopore/markd/t2t-centromere/T2T_cenSat"

##
# output directories
##
hubDir = osp.join(resultsDir, "hub")
dataDir = osp.join(resultsDir, "data")
asmHubDir = osp.join(hubDir, asmName)

cenSatWgDir = "/public/groups/nanopore/karen/chm13/cenSat_WG"
asmSrcDir = osp.join(cenSatWgDir, "assembly")
phase1SrcDir = osp.join(cenSatWgDir, "phase1/browser")

##
# hub files
##
hubSrcDir = "../../hub"
def findHubFiles():
    prelen = len(Path(hubSrcDir).parts)
    return [osp.join(*p.parts[prelen:])
            for  p in (list(Path(hubSrcDir).rglob("*.txt"))
                       + list(Path(hubSrcDir).rglob("*.html")))]
hubFiles = findHubFiles()
hubInFiles = [osp.join(hubSrcDir, f) for f in hubFiles]
hubOutFiles = [osp.join(hubDir, f) for f in hubFiles]

##
# inputs (need in multiple places)
##
asmSeqs = osp.join(asmHubDir, asmName + ".seqs")
centroRegionBeds = glob.glob(osp.join(asmSrcDir, "chr*/cen*_entireALR.bed"))
contigs = list(fileOps.readFileLines(asmSeqs))

##
# results
##
asmTwoBit = osp.join(asmHubDir, asmName + ".2bit")
asmSizes = osp.join(asmHubDir, asmName + ".sizes")
centroRegionsBigBed = osp.join(asmHubDir, "centroRegions.bigBed")
rmskBigBed = osp.join(asmHubDir, "rmsk.bigBed")
horArrayBigBed = osp.join(asmHubDir, "horArray.bigBed")
horHmmBigBed = osp.join(asmHubDir, "horHmm.bigBed")
allGenomeSFBigBed = osp.join(asmHubDir, "allGenomeSF.bigBed")
sf1HorBigBed = osp.join(asmHubDir, "sf1Hor.bigBed")
pubMonBigBed = osp.join(asmHubDir, "pubMon.bigBed")

allResults = [asmTwoBit, asmSizes, centroRegionsBigBed,
              rmskBigBed, horArrayBigBed, horHmmBigBed, allGenomeSFBigBed,
              sf1HorBigBed, pubMonBigBed] + hubOutFiles

##
# misc defs
##
bedSortCmd = ["sort", "-k1,1", "-k2,2n"]
binDir = "../../bin"
editRepeatMaskerBed = osp.join(binDir, "editRepeatMaskerBed")
RM2Bed = osp.join(binDir, "RM2Bed.py")

##
# functions to do common stuff
##
def buildBigBed(inBeds, outBigBed, *, bedType="bed4", filterCmd=None):
    if isinstance(inBeds, str):
        inBeds = [inBeds]
    with tempfile.NamedTemporaryFile() as bedFh:
        # discard non-cenSat contigs
        cmd = [["selectById", 1, asmSeqs, 1] + inBeds,
               bedSortCmd]
        if filterCmd is not None:
            cmd.append(filterCmd)
        pipettor.run(cmd, stdout=bedFh.name)
        pipettor.run(["bedToBigBed", "-type=" + bedType, "-tab", bedFh.name, asmSizes, outBigBed])

###
# rule that drives everything
###

rule all:
    input: allResults

rule clean:
    run:
        fileOps.rmFiles(*allResults)



###
# genome file
###
genomeFasta = osp.join(asmSrcDir, "chm13_hicanu_hifi_20k.fasta")

rule genomeSeqs:
    input: genomeFasta, centroRegionBeds, asmSeqs
    output: asmTwoBit, asmSizes
    run:
        pipettor.run([("faSomeRecords", genomeFasta, asmSeqs, "/dev/stdout"),
                      ("faToTwoBit", "/dev/stdin", asmTwoBit)])
        pipettor.run(["twoBitInfo", asmTwoBit, asmSizes])


###
# copy hub files
###
rule hubText:
    input: hubInFiles
    output: hubOutFiles
    run:
        for i in range(len(input)):
            shutil.copyfile(input[i], output[i])

###
# centromere region track
###
rule centroRegionsTrack:
    input: centroRegionBeds, asmSizes
    output: centroRegionsBigBed
    run:
        buildBigBed(centroRegionBeds, centroRegionsBigBed)


##
# repeat masker
##
rmskDir = osp.join(asmSrcDir, "rm")
rmskOutFiles = [glob.glob(osp.join(rmskDir, tig + ".out/*.fasta.out"))[0]
                for tig in contigs]
rule rmsk:
    input: rmskOutFiles, asmSeqs, RM2Bed, editRepeatMaskerBed
    output: rmskBigBed
    run:
        # regions are named in the form tig00018121:0-1387635,
        # but cover the whole contig
        with tempfile.NamedTemporaryFile() as bedFh:
            for f in rmskOutFiles:
                pipettor.run([[RM2Bed, "--stdout", "--log_level=WARN", f],
                              [editRepeatMaskerBed]], stdout=bedFh)
            buildBigBed([bedFh.name], rmskBigBed, bedType="bed9")

##
# HOR arrays, etc
##
horArrayBed = osp.join(phase1SrcDir, "cenSat_HORarray.bed")
rule horArray:
    input: horArrayBed, asmSizes
    output: horArrayBigBed
    run:
        buildBigBed([horArrayBed], horArrayBigBed)

horHmmBed = osp.join(phase1SrcDir, "cenSat_HOR_hmmOut.bed")
rule horHmm:
    input: horHmmBed, asmSizes
    output: horHmmBigBed
    run:
        buildBigBed(horHmmBed, horHmmBigBed, filterCmd=["cut", "-f", "1-4"])

allGenomeSFBed = osp.join(phase1SrcDir, "all_genome_SF_track.chm13_hicanu_hifi_20k.bed")
rule allGenomeSF:
    input: allGenomeSFBed, asmSizes
    output: allGenomeSFBigBed
    run:
        buildBigBed(allGenomeSFBed, allGenomeSFBigBed, bedType="bed9")

sf1HorBed = osp.join(phase1SrcDir, "SF1_HOR_track.chm13_hicanu_hifi_20k.bed")
rule sf1Hor:
    input: sf1HorBed, asmSizes
    output: sf1HorBigBed
    run:
        buildBigBed(sf1HorBed, sf1HorBigBed, bedType="bed9")

pubMonBed = osp.join(phase1SrcDir, "pubMon_track.chm13_hicanu_hifi_20k.bed")
rule pubMon:
    input: pubMonBed, asmSizes
    output: pubMonBigBed
    run:
        buildBigBed(pubMonBed, pubMonBigBed, bedType="bed9")
